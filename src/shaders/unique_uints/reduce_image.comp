#version 460
#include <triangle_id.glsl>

layout (set = 0, binding = 0) uniform usampler2D ID_IMAGE;

layout (set = 0, binding = 1) buffer OutBuckets {
  uint IDS[];
};

layout (set = 0, binding = 2) buffer TrianglesCountBuffer {
  uint TRIANGLE_COUNT[];
};

layout (push_constant) uniform PushConstants {
  uint BUCKETS_COUNT;
};

void hash_insert(uint value);

const uint INVALID_VAL = ~0u;

//32x32 blocks
const uint ELEMS_COUNT = 32 * 32;
shared uint HASH_TABLE[ELEMS_COUNT];

layout (local_size_x = 32, local_size_y = 32) in;
void main() {

  HASH_TABLE[gl_LocalInvocationIndex] = INVALID_VAL;

  barrier();

  ivec2 tex_size = ivec2(textureSize(ID_IMAGE, 0).xy);

  ivec2 pixel_pos = ivec2(gl_WorkGroupID.xy * gl_WorkGroupSize.xy + gl_LocalInvocationID.xy);
  
  if (!all(greaterThanEqual(pixel_pos, tex_size))) {
    hash_insert(texelFetch(ID_IMAGE, pixel_pos, 0).x);
  }

  barrier();

  uint resolve_id = HASH_TABLE[gl_LocalInvocationIndex];
  if (resolve_id != INVALID_VAL) {
    uint hash = lowbias32(resolve_id);
    hash = hash % BUCKETS_COUNT;
    uint index = atomicAdd(TRIANGLE_COUNT[hash], 1);
    IDS[hash * ELEMS_COUNT + index] = resolve_id;
  }
}

void hash_insert(uint value) {
  if (value == INVALID_VAL)
    return;
  uint bucket = value & (ELEMS_COUNT - 1);
  uint prev = 0;

  do {
    prev = atomicCompSwap(HASH_TABLE[bucket], INVALID_VAL, value);
    bucket = (bucket + 1) & (ELEMS_COUNT - 1);
  } while (prev != INVALID_VAL && prev != value);
}
